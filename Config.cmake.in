#https://cmake.org/cmake/help/latest/manual/cmake-packages.7.html#creating-a-package-configuration-file

@PACKAGE_INIT@
include(CMakeFindDependencyMacro)
find_package(QT NAMES Qt6 Qt5 REQUIRED COMPONENTS Core)
find_package(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS Core)

set(QtLab_INCLUDE_DIR @CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_INCLUDEDIR@)

set(_supported_components
    Core
    IO
    Hamamatsu
    NI
    PI
    PI-Widgets
    Serial
    Serial-Widgets
    ThorlabsMC
    Widgets
)

foreach(_comp ${_supported_components})
  if (";${QtLab_FIND_COMPONENTS};" MATCHES ${_comp})
    include("${CMAKE_CURRENT_LIST_DIR}/${_comp}Targets.cmake"
            RESULT_VARIABLE QtLab_${_comp}_FOUND)
  endif()
endforeach()

if(";${QtLab_FIND_COMPONENTS};" MATCHES Widgets)
    find_dependency(Qt5 REQUIRED Widgets)
endif()

if(";${QtLab_FIND_COMPONENTS};" MATCHES Serial)
find_package(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS SerialPort)
endif()

set(QtLab_FOUND True)

foreach(_comp ${QtLab_FIND_COMPONENTS})
  if (NOT ";${_supported_components};" MATCHES ${_comp})
    set(QtLab_FOUND False)
    message(STATUS "Unsupported component: ${_comp}")
    set(QtLab_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
  endif()
endforeach()

check_required_components(QtLab)

if(NOT ${QtLab_FOUND})
   message(STATUS "Could not locate QtLab")
endif()
